import java.util.Properties

plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
    id("org.jetbrains.kotlin.plugin.compose")
    id("org.jetbrains.kotlin.plugin.serialization")
    id("com.google.devtools.ksp")
}

android {
    namespace = "tv.safetubeforkids.app"
    compileSdk = 36

    defaultConfig {
        applicationId = "tv.safetubeforkids.app"
        minSdk = 24
        targetSdk = 34
        versionCode = 15
        versionName = "0.9.3"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "RELAY_URL", "\"https://relay.parentapproved.tv\"")
        buildConfigField("int", "PROTOCOL_VERSION", "1")
        buildConfigField("String", "VERSION_CHECK_URL", "\"\"")
    }

    signingConfigs {
        create("release") {
            val localProps = rootProject.file("local.properties").let { file ->
                if (file.exists()) Properties().also { it.load(file.inputStream()) } else Properties()
            }
            fun prop(name: String): String? = localProps.getProperty(name) ?: System.getenv(name)

            storeFile = file(prop("RELEASE_STORE_FILE") ?: "nonexistent.keystore")
            storePassword = prop("RELEASE_STORE_PASSWORD") ?: ""
            keyAlias = prop("RELEASE_KEY_ALIAS") ?: ""
            keyPassword = prop("RELEASE_KEY_PASSWORD") ?: storePassword
        }
    }

    buildTypes {
        debug {
            buildConfigField("Boolean", "IS_DEBUG", "true")
        }
        release {
            isMinifyEnabled = false
            signingConfig = signingConfigs.getByName("release")
            buildConfigField("Boolean", "IS_DEBUG", "false")
        }
    }

    compileOptions {
        isCoreLibraryDesugaringEnabled = true
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    buildFeatures {
        compose = true
        buildConfig = true
    }

    testOptions {
        unitTests.isReturnDefaultValues = true
    }

    packaging {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
            excludes += "/META-INF/INDEX.LIST"
            excludes += "/META-INF/io.netty.versions.properties"
        }
    }
}

# Name the built APK after the app, not the project directory name.
base.archivesName.set("SafeTubeforKids")

dependencies {
    val composeBom = platform("androidx.compose:compose-bom:2026.02.01")
    implementation(composeBom)

    implementation("androidx.core:core-ktx:1.12.0")
    implementation("androidx.activity:activity-compose:1.12.4")

    // Compose
    implementation("androidx.compose.ui:ui")
    implementation("androidx.compose.ui:ui-tooling-preview")
    implementation("androidx.compose.material3:material3")
    implementation("androidx.compose.material:material-icons-extended")
    implementation("androidx.compose.foundation:foundation")

    // TV-specific
    implementation("androidx.tv:tv-material:1.0.0")
    implementation("androidx.leanback:leanback:1.0.0")

    // Navigation
    implementation("androidx.navigation:navigation-compose:2.8.5")

    // Lifecycle
    implementation("androidx.lifecycle:lifecycle-runtime-ktx:2.7.0")
    implementation("androidx.lifecycle:lifecycle-viewmodel-compose:2.7.0")

    // Network
    implementation("com.squareup.okhttp3:okhttp:4.12.0")

    // Desugaring (needed by NewPipeExtractor)
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs_nio:2.1.5")

    // NewPipe Extractor
    // v0.26.5 is required for YouTube playlist extraction: YouTube now returns playlist
    // items as "lockupViewModel" in an appendContinuationItemsAction, which v0.25.2
    // ignores (it only knows playlistVideoRenderer/richItemRenderer/reelItemRenderer).
    // Result on v0.25.2: playlists resolve with 0 videos and no error.
    // Fixed upstream in v0.26.3 ("playlist items ... in lockup view models") and
    // v0.26.4 ("Fix fetching playlists continuations").
    implementation("com.github.teamnewpipe:NewPipeExtractor:v0.26.5")

    // Media3 ExoPlayer — pinned to the current stable release for track selection,
    // DASH multi-track playback and subtitle support used by the TV player.
    val media3Version = "1.11.1"
    implementation("androidx.media3:media3-exoplayer:$media3Version")
    implementation("androidx.media3:media3-exoplayer-dash:$media3Version")
    implementation("androidx.media3:media3-ui:$media3Version")

    // Ktor server
    val ktorVersion = "3.1.1"
    implementation("io.ktor:ktor-server-core:$ktorVersion")
    implementation("io.ktor:ktor-server-netty:$ktorVersion")
    implementation("io.ktor:ktor-server-content-negotiation:$ktorVersion")
    implementation("io.ktor:ktor-serialization-kotlinx-json:$ktorVersion")
    implementation("io.ktor:ktor-server-sessions:$ktorVersion")
    implementation("io.ktor:ktor-server-status-pages:$ktorVersion")
    implementation("io.ktor:ktor-server-cors:$ktorVersion")
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.2")

    // QR code
    implementation("com.google.zxing:core:3.5.2")

    // Room (cache + play events)
    val roomVersion = "2.8.4"
    implementation("androidx.room:room-runtime:$roomVersion")
    implementation("androidx.room:room-ktx:$roomVersion")
    ksp("androidx.room:room-compiler:$roomVersion")

    // Coil (image loading)
    implementation("io.coil-kt:coil-compose:2.7.0")

    debugImplementation("androidx.compose.ui:ui-tooling")

    // Unit test dependencies
    testImplementation("junit:junit:4.13.2")
    testImplementation("io.mockk:mockk:1.13.8")
    testImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3")
    testImplementation("com.squareup.okhttp3:mockwebserver:4.12.0")
    testImplementation("io.ktor:ktor-server-test-host:$ktorVersion")
    testImplementation("io.ktor:ktor-client-content-negotiation:$ktorVersion")
    testImplementation("androidx.room:room-testing:$roomVersion")

    // Instrumented test dependencies
    androidTestImplementation("androidx.test.ext:junit:1.1.5")
    androidTestImplementation("androidx.test:runner:1.7.0")
    androidTestImplementation("com.squareup.okhttp3:okhttp:4.12.0")
}
